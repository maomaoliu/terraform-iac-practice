apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.8"
}

ext {
    jacocoLineMin = 0.85
    jacocoBranchMin = 0.70
    jacocoExcludes = [
            "**/archunit",
            "**/BootstrapApplication*",
            "**/exception/**",
            "**/config/**",
            "**/*Mapper.class",
            "**/*MapperImpl.class"
    ]
}

jacocoTestReport {
    group = 'verification'
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        })
    }
}

jacocoTestCoverageVerification {
    group = 'verification'
    shouldRunAfter jacocoTestReport
    executionData test
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(sourceSets.main.output)

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludes)
        })
    }

    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'INSTRUCTION'
                minimum = jacocoLineMin
            }
        }

        rule {
            element = 'BUNDLE'
            limit {
                counter = 'BRANCH'
                minimum = jacocoBranchMin
            }
        }
    }
}

jacocoTestReport.dependsOn test
jacocoTestCoverageVerification.dependsOn jacocoTestReport
check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
